FUNCTION sh_whichband,a,num=num,linedata=linedata
  b   = ['1a','1b','1d','1e','2a','2b','2c','3a','3c','3d','3e','4a','4c','4d', '4','unknown']
  lin = [   1,   2,   3,   4,   5,   6,   7,   9,  10,  11,  12,  20,  21,  22,  13,0]   
  dlo = [   1,   1,   1,   1,  13,  13,  13,  25,  25,  25,  25,  37,  37,  37,  37,0]*1L
  dhi = [  12,  12,  12,  12,  24,  24,  24,  36,  36,  36,  36,  48,  48,  48,  48,1000]*1L
  flg = [ 128,  96,  96,  64,  64,  32,  32,  64,  64,  32,  32,  64,  64,  32,  32,0]*1L
  sts = [   1,   1,   2,   2,   2,   2,   3,   1,   2,   2,   3,   1,   2,   2,   3,0]*1L
  
  ;; standard return values
  num =0
  linedata = a.data.line*0
  
  IF n_params() EQ 0 THEN BEGIN
    print,'SH_WHICHBAND: Usage t = whichband(aar)'
    return,0
  END
  
  d = a.data.det
  f = a.data.flag
  s = a.data.status
  
  ;; These are arrays of dimensions of (d/f/s) and (lin/dlo/..) filled
  ;; with 1s
  d1 = (d EQ d)
  flg1 = (flg EQ flg)
  
  ;; Stretch the whole rataplan
  dd = d##flg1
  ff = f##flg1
  ss = s##flg1
  
  linlin = d1##lin
  dlodlo = d1##dlo
  dhidhi = d1##dhi
  flgflg = d1##flg
  stssts = d1##sts
  
  outout = (dlodlo LE dd) AND (dhidhi GE dd) AND ((ff AND 224L) EQ flgflg) AND ((ss AND 3L) EQ stssts)
  idxband = where(total(outout,2))
  IF idxband(0) EQ -1 THEN return,b[15]
  linedata = reform((outout##lin),n_elements(d1))
  num = lin[idxband]
  return,b[idxband]
END
